plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id "org.jetbrains.kotlin.kapt" version "1.7.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'one.oktw'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = "https://jitpack.io" }
    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    kapt 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    implementation 'io.fabric8:kubernetes-client:5.12.2'
    implementation 'com.github.james58899:k8s-kotlin-dsl:6709c28a59'
    implementation 'io.lettuce:lettuce-core:6.2.0.RELEASE'
    implementation 'one.oktw:galaxy-lib:7fb749bc'
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

shadowJar {
    zip64 true
    archiveClassifier.set('')
    minimize {
        exclude(dependency('io.fabric8:kubernetes-client:.*'))
    }
    exclude '*.aut'
    exclude '*.properties'
    exclude 'OSGI-INF'
    exclude 'schema'
}

jar.enabled = false
build.dependsOn(shadowJar)
