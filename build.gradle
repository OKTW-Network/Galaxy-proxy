plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id "org.jetbrains.kotlin.kapt" version "1.7.10"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'one.oktw'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = "https://jitpack.io" }
    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    kapt 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    implementation 'io.fabric8:kubernetes-client:6.3.1'
    implementation 'io.fabric8:kubernetes-httpclient-okhttp:6.0.0-RC1'
    implementation 'com.github.fkorotkov:k8s-kotlin-dsl:3.2.0'
    implementation 'io.lettuce:lettuce-core:6.2.0.RELEASE'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactive' // lettuce need this
    implementation 'one.oktw:galaxy-lib:56fb909a'
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

shadowJar {
    archiveClassifier.set('')
    minimize {
        exclude(dependency('io.fabric8:kubernetes-client:.*'))
    }
    exclude '*.aut'
    exclude '*.properties'
    exclude 'OSGI-INF'
    exclude 'schema'
}

jar.enabled = false
build.dependsOn(shadowJar)
