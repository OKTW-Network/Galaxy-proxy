import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id "org.jetbrains.kotlin.kapt" version "2.0.21"
    id 'com.gradleup.shadow' version '8.3.5'
}

group 'one.oktw'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = "https://jitpack.io" }
    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name 'adventure'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    kapt 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    compileOnly 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    implementation 'io.fabric8:kubernetes-client:7.3.1'
    implementation 'io.fabric8:kubernetes-httpclient-okhttp:6.13.4'
    implementation 'com.github.fkorotkov:k8s-kotlin-dsl:3.3.0'
    implementation 'io.lettuce:lettuce-core:6.5.0.RELEASE'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactive' // lettuce need this
    implementation 'one.oktw:galaxy-lib:c328418'
}

compileKotlin {
    compilerOptions.jvmTarget = JvmTarget.JVM_21
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

shadowJar {
    archiveClassifier.set('')
    minimize {
        exclude(dependency('io.fabric8:kubernetes-client:.*'))
    }
    exclude '*.aut'
    exclude '*.properties'
    exclude 'OSGI-INF'
    exclude 'schema'
}

jar.enabled = false
build.dependsOn(shadowJar)
