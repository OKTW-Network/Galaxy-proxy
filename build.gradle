buildscript {
    ext.kotlin_version = '1.3.21'
    ext.Coroutines_version = "1.1.1"
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "org.jetbrains.kotlin.kapt" version "1.3.21"
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group 'one.oktw'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven {
        name 'velocity'
        url 'https://repo.velocitypowered.com/snapshots/'
    }
}

dependencies {
    kapt 'com.velocitypowered:velocity-api:1.0-SNAPSHOT'
    implementation project(':api')
    implementation 'com.velocitypowered:velocity-api:1.0-SNAPSHOT'
    implementation 'io.kubernetes:client-java:4.0.0'
    implementation 'io.lettuce:lettuce-core:5.1.4.RELEASE'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$Coroutines_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

shadowJar {
    zip64 true
    classifier = null
    exclude "META-INF/**"
    exclude "**/*.kotlin_metadata"

    // exclude velocity dependencies
    dependencies {
        include dependency(":api")
        include dependency("org.mongodb:")
        include dependency("io.kubernetes:")
        include dependency("org.jetbrains.kotlin:")
        include dependency("org.jetbrains.kotlinx:")
        include dependency("org.apache.commons:")
        include dependency("com.squareup.okhttp:")
        include dependency("com.squareup.okio:")
        include dependency("joda-time:")
        include dependency("io.lettuce:")
        include dependency("io.projectreactor:")
        include dependency("io.netty:")
        include dependency("org.reactivestreams:")
    }

    relocate "kotlin", "one.oktw.relocate.kotlin"
    relocate "kotlinx", "one.oktw.relocate.kotlinx"
    relocate "org.bson", "one.oktw.relocate.bson"
    relocate "org.jetbrains", "one.oktw.relocate.org.jetbrains"
    relocate "org.joda", "one.oktw.relocate.org.joda"
    relocate "org.apache", "one.oktw.relocate.org.apache"
    relocate "org.reactivestreams", "one.oktw.relocate.org.reactivestreams"
    relocate "io.kubernetes", "one.oktw.relocate.io.kubernetes"
    relocate "io.lettuce", "one.oktw.relocate.io.lettuce"
    relocate "io.netty", "one.oktw.relocate.io.netty"
    relocate "com.squareup", "one.oktw.relocate.com.squareup"
    relocate "okio", "one.oktw.relocate.okio"
    relocate "reactor", "one.oktw.relocate.reactor"

    minimize()
}

jar.enabled = false
build.dependsOn(shadowJar)
